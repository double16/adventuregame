plugins {
    id 'org.springframework.boot' version '2.2.2.RELEASE'
    id 'java'
    id 'groovy'
    id 'com.github.ben-manes.versions' version '0.27.0'
    id 'ru.vyarus.quality' version '4.0.0'
    id 'com.bmuschko.clover' version '2.2.4'
    id 'org.owasp.dependencycheck' version '5.2.4'
    id 'com.patdouble.cucumber-jvm' version '0.19'
    id 'org.sonarqube' version '2.8'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'com.bmuschko.docker-spring-boot-application' version '6.0.0'
}

apply plugin: 'io.spring.dependency-management'

group = 'org.patdouble.adventuregame'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.codehaus.groovy:groovy-all:2.5.+'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-rest'
    implementation 'org.springframework.boot:spring-boot-starter-hateoas'
//    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
//    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'org.springframework.data:spring-data-rest-hal-explorer'
    implementation 'com.github.yingzhuo:carnival-spring-boot-starter-gravatar:1.3.3'
    implementation 'org.yaml:snakeyaml:1.25'
    implementation 'org.webjars:webjars-locator-core'
    implementation 'org.webjars:bootstrap:4.4.1'
    implementation 'org.webjars.npm:vue:2.6.10'
    implementation 'org.webjars.npm:rxjs:6.5.3'
    implementation 'org.webjars.npm:dagre:0.8.4'
    implementation 'org.webjars.npm:dagre-d3:0.6.3'
    implementation 'org.webjars.npm:axios:0.19.0'
    implementation 'org.webjars:font-awesome:5.11.2'
    implementation 'org.webjars:sockjs-client:1.1.2'
    implementation 'org.webjars:stomp-websocket:2.3.3-1'
    implementation 'org.kie:kie-api:7.28.0.Final'
    implementation 'org.drools:drools-compiler:7.28.0.Final'
    implementation 'org.fusesource.jansi:jansi:1.18'
    implementation 'org.jline:jline:3.13.2'
    runtimeOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'org.postgresql:postgresql'
    runtimeOnly 'com.h2database:h2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'org.springframework.security:spring-security-test'

    // Spock
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.spockframework:spock-core:1.3-RC1-groovy-2.5'
    testImplementation 'org.spockframework:spock-spring:1.3-RC1-groovy-2.5'
    testRuntimeOnly 'cglib:cglib-nodep:3.3.0'

    clover 'org.openclover:clover:4.4.1'
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'ea'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]$qualifier[.\d-+]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

shadowJar {
    manifest {
        attributes 'Main-Class': 'org.patdouble.adventuregame.ui.console.Main'
    }
}

task trailerPark(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'org.patdouble.adventuregame.ui.console.Main'
    standardInput = System.in
}

docker {
    springBootApplication {
        baseImage = 'openjdk:11.0.4-jre-slim'
        maintainer = 'Patrick Double "pat@patdouble.com"'
        images = ["pdouble16/${project.name}:${project.version}"]
        // jvmArgs = ['-Dspring.profiles.active=production', '-Xmx2048m']
    }
}

clover {
    report {
        html = true
        xml = true
    }
}
