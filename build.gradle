plugins {
    id 'org.springframework.boot' version '2.2.4.RELEASE'
    id 'java'
    id 'groovy'
    id 'idea'
    id 'com.github.ben-manes.versions' version '0.27.0'
    id 'ru.vyarus.quality' version '4.0.0'
    id 'com.bmuschko.clover' version '2.2.4'
    id 'org.owasp.dependencycheck' version '5.3.0'
    id 'com.patdouble.cucumber-jvm' version '0.19'
    id 'org.sonarqube' version '2.8'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'com.bmuschko.docker-spring-boot-application' version '6.1.2'
}

apply plugin: 'io.spring.dependency-management'

group = 'org.patdouble.adventuregame'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
    mavenCentral()
}

ext {
    droolsVersion = '7.28.0.Final'
}

dependencies {
    implementation 'org.codehaus.groovy:groovy-all:2.5.+'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-rest'
    implementation 'org.springframework.boot:spring-boot-starter-hateoas'
//    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
//    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'org.springframework.data:spring-data-rest-hal-explorer'
    implementation 'com.github.yingzhuo:carnival-spring-boot-starter-gravatar:1.4.+'
    implementation 'org.yaml:snakeyaml:1.25'
    implementation 'org.webjars:webjars-locator-core'
    implementation 'org.webjars:bootstrap:4.4.1'
    implementation 'org.webjars.npm:vue:2.6.11'
    implementation 'org.webjars.npm:rxjs:6.5.3'
    implementation 'org.webjars.npm:dagre:0.8.+'
    implementation 'org.webjars.npm:dagre-d3:0.6.+'
    implementation 'org.webjars.npm:axios:0.19.+'
    implementation 'org.webjars:font-awesome:5.12.+'
    implementation 'org.webjars:sockjs-client:1.1.2'
    implementation 'org.webjars:stomp-websocket:2.3.3-1'
    implementation "org.kie:kie-spring:${droolsVersion}"
    implementation "org.kie:kie-ci:${droolsVersion}"
    implementation "org.drools:drools-core:${droolsVersion}"
    implementation "org.drools:drools-compiler:${droolsVersion}"
    implementation 'org.luaj:luaj-jse:3.0.1' // github.com/luaj/luaj
    implementation 'org.fusesource.jansi:jansi:1.18'
    implementation 'org.jline:jline:3.13.+'
    runtimeOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'org.postgresql:postgresql'
    runtimeOnly 'com.h2database:h2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'org.springframework.security:spring-security-test'

    // Spock
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
    testImplementation 'org.spockframework:spock-spring:1.3-groovy-2.5'
    testRuntimeOnly 'cglib:cglib-nodep:3.3.0'

    clover 'org.openclover:clover:4.4.1'
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'ea'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]$qualifier[.\d-+]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

check.dependsOn dependencyCheckAggregate

plugins.withId('groovy') {
    compileGroovy {
        groovyOptions.optimizationOptions.indy = true
    }
}

idea {
    project {
        jdkName = '11'
        languageLevel = '11'
        vcs = 'Git'
    }
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

shadowJar {
    zip64 = true
    manifest {
        attributes 'Main-Class': 'org.patdouble.adventuregame.ui.console.Main'
    }
    mergeServiceFiles()
    mergeGroovyExtensionModules()
    append 'META-INF/kie.conf'
}

task trailerPark(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'org.patdouble.adventuregame.ui.console.Main'
    standardInput = System.in
}

docker {
    springBootApplication {
        baseImage = 'openjdk:11.0.4-jre-slim'
        maintainer = 'Patrick Double "pat@patdouble.com"'
        images = ["pdouble16/${project.name}:${project.version}"]
        // jvmArgs = ['-Dspring.profiles.active=production', '-Xmx2048m']
    }
}

clover {
    report {
        html = true
        xml = true
    }
}

afterEvaluate {
    codenarc {
        maxPriority1Violations = 0
        maxPriority2Violations = 0
        maxPriority3Violations = 200
    }
}
