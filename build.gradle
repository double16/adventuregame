plugins {
    id 'org.springframework.boot' version '2.6.2'
    id 'java'
    id 'groovy'
    id 'idea'
    id 'com.github.ben-manes.versions' version '0.39.0'
    id 'ru.vyarus.quality' version '4.7.0'
    id 'com.bmuschko.clover' version '3.0.2'
    id 'org.owasp.dependencycheck' version '6.5.0.1'
    id 'com.patdouble.cucumber-jvm' version '0.20'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'info.solidsoft.pitest' version '1.7.0'
    id 'de.undercouch.download' version '4.1.2'
    id 'com.patdouble.jmeter-gradle-plugin' version '1.3.0-5.4.1'
    id 'uk.co.automatictester.lightning' version '2.0.1'
    id 'com.patdouble.zap' version '2.11.0'
    id 'com.avast.gradle.docker-compose' version '0.14.11'
}

apply plugin: 'io.spring.dependency-management'
apply from: 'gradle/jmeter.gradle'
apply from: 'gradle/zap.gradle'
apply from: 'gradle/cucumber.gradle'

group = 'org.patdouble.adventuregame'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
    mavenCentral()
}

ext {
    droolsVersion = '7.63.0.Final'
    groovyVersion = '2.5.+'
//    groovyVersion = '3.0.+'
}

dependencies {
    implementation "org.codehaus.groovy:groovy:${project.groovyVersion}"
    implementation "org.codehaus.groovy:groovy-templates:${project.groovyVersion}"
    implementation 'javax.validation:validation-api:2.0.1.Final'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-rest'
    implementation 'org.springframework.boot:spring-boot-starter-hateoas'
//    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
//    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    developmentOnly 'org.springframework.data:spring-data-rest-hal-explorer'
//    implementation 'com.github.yingzhuo:carnival-spring-boot-starter-gravatar:1.4.+'
    implementation 'org.yaml:snakeyaml:1.30'
    implementation 'org.webjars:webjars-locator-core'
    implementation 'org.webjars:bootstrap:4.5.+'
    implementation 'org.webjars.npm:vue:2.6.+'
    implementation 'org.webjars.npm:vue-router:3.5.+'
    implementation 'org.webjars.npm:rxjs:6.6.+'
    implementation 'org.webjars.npm:dagre-d3:0.6.+'
    implementation 'org.webjars.npm:graphlib-dot:0.6.+'
    implementation 'org.webjars.npm:axios:0.21.+'
    implementation 'org.webjars:font-awesome:5.15.+'
    implementation 'org.webjars.npm:stomp__stompjs:5.4.+'
    implementation "org.kie:kie-spring:${droolsVersion}"
    // Supports hot reloading of rules from a Maven repo: implementation "org.kie:kie-ci:${droolsVersion}"
    implementation "org.drools:drools-core:${droolsVersion}"
    implementation "org.drools:drools-compiler:${droolsVersion}"
    implementation 'org.luaj:luaj-jse:3.0.1' // github.com/luaj/luaj
    implementation 'org.fusesource.jansi:jansi:2.4.+'
    implementation 'org.jline:jline:3.21.+'
    implementation 'org.codehaus.gpars:gpars:1.2.+'
    implementation 'commons-io:commons-io:2.11.+'
    implementation 'org.apache.commons:commons-lang3:3.12.+'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'org.postgresql:postgresql'
    runtimeOnly 'com.h2database:h2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
//    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.codehaus.gpars:gpars:1.2.1'
    testImplementation 'io.github.java-diff-utils:java-diff-utils:4.11'

    // Spock
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    if (project.groovyVersion.startsWith('3')) {
        testImplementation 'org.spockframework:spock-core:2.0-groovy-3.0'
        testImplementation 'org.spockframework:spock-spring:2.0-groovy-3.0'
    } else {
        testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
        testImplementation 'org.spockframework:spock-spring:1.3-groovy-2.5'
    }
    testRuntimeOnly 'cglib:cglib-nodep:3.3.0'

    clover 'org.openclover:clover:4.4.1'
}

configurations.all {
    resolutionStrategy {
        dependencySubstitution {
            all { DependencySubstitution dependency ->
                if (dependency.requested instanceof ModuleComponentSelector
                        && dependency.requested.group == 'org.codehaus.groovy'
                        && dependency.requested.module.startsWith('groovy')) {
                    dependency.useTarget "${dependency.requested.group}:${dependency.requested.module}:${project.groovyVersion}"
//                    dependency.useTarget "org.codehaus.groovy:groovy-all:${project.groovyVersion}"
                }
            }
        }
    }
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'ea'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]$qualifier[.\d-+]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

check.dependsOn dependencyCheckAggregate
dependencyCheck {
    skipConfigurations = [
            'developmentOnly',
            'testImplementation',
            'testRuntimeOnly',
            'clover',
            'codenarc',
            'cucumberTestCompile',
            'cucumberTestRuntimeOnly',
    ]
}

plugins.withId('groovy') {
    compileGroovy {
        groovyOptions.optimizationOptions.indy = true
    }
}

idea {
    project {
        jdkName = '11'
        languageLevel = '11'
        vcs = 'Git'
    }
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

processResources {
    from('src/main/webapp') {into 'static'}
}

shadowJar {
    zip64 = true
    manifest {
        attributes 'Main-Class': 'org.patdouble.adventuregame.ui.console.Main'
    }
    mergeServiceFiles()
    mergeGroovyExtensionModules()
    append 'META-INF/kie.conf'
}

task trailerPark(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'org.patdouble.adventuregame.ui.console.Main'
    standardInput = System.in
}

bootRun {
    // Allow html/js resources to be reloaded, from:
    // https://docs.spring.io/spring-boot/docs/2.2.6.RELEASE/gradle-plugin/reference/html/#running-your-application-reloading-resources
    sourceResources sourceSets.main
}

bootBuildImage {
    imageName = "pdouble16/${project.name}"
    targetJavaVersion = JavaVersion.VERSION_15
//    instruction 'HEALTHCHECK CMD curl -s -O /dev/null http://localhost:8080/actuator/health || exit 1'
}

composeUp.dependsOn bootBuildImage

clover {
    report {
        html = true
        xml = true
    }
    excludeTasks = ['cucumberTest']
}

afterEvaluate {
    codenarc {
        maxPriority1Violations = 0
        maxPriority2Violations = 50
        maxPriority3Violations = 200
        configFile = project.file('config/codenarc/main.groovy')
    }
}

pitest {
    // https://github.com/hcoles/pitest
    pitestVersion = '1.7.2'
    timestampedReports = false
}
