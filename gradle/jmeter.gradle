jmeter {
    enableReports = false
    enableExtendedReports = true
}

lightningSettings {
    testSetXml = project.file('src/test/jmeter/lightning.xml')
}

dockerCompose.isRequiredBy(jmRun)

def configureJMeterUserProperties = {
    def host = dockerCompose.servicesInfos.app.firstContainer.host
    def port = dockerCompose.servicesInfos.app.firstContainer.ports[8080]
    URL url = new URL("http://${host}:${port}")
    if (jmeter.jmUserProperties == null) {
        jmeter.jmUserProperties = []
    }
    jmeter.jmUserProperties << "app_proto=${url.protocol}"
    jmeter.jmUserProperties << "app_host=${url.host}"
    jmeter.jmUserProperties << "app_port=${url.port}"
    jmeter.jmUserProperties << "app_path=${url.path}"
}
jmRun.doFirst(configureJMeterUserProperties)
jmRun.doLast {
    project.lightningSettings.jmeterCsv = project.jmeter.jmResultFiles.first()
}

[tasks.report, tasks.verify]*.dependsOn jmRun
verify.doLast {
    File junitOrig = project.file('junit.xml')
    File junitDest = new File(project.buildDir, "test-results/${project.name}/LOAD-junit.xml")
    if (junitOrig.exists()) {
        junitDest.parentFile.mkdirs()
        junitOrig.renameTo(junitDest)
    }
}
