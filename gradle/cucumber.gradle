
addCucumberSuite 'cucumberTest'

//dockerCompose.isRequiredBy(cucumberTest)

cucumber {
    maxParallelForks = 1
    junitReport = true
}

cucumberTest {
    stepDefinitionRoots = ['cucumber.steps', 'cucumber.hooks'] // default

    doFirst {
        def host = '127.0.0.1'
        def port = 8080
        if (dockerCompose.servicesInfos) {
            host = dockerCompose.servicesInfos.app.firstContainer.host
            port = dockerCompose.servicesInfos.app.firstContainer.ports[8080]
        }
        URL url = new URL("http://${host}:${port}")

        systemProperties = [
                'app_proto': url.protocol,
                'app_host': url.host,
                'app_port': url.port,
                'app_path': url.path,
                'geb.build.baseUrl': url.toExternalForm(),
                'selenium.version' : seleniumVersion,
                'selenium.chromedriver.version' : seleniumChromeDriverVersion,

        ]
    }
}

dependencies {
    cucumberTestImplementation 'io.cucumber:cucumber-java:6.10.+'
    // To use JUnit assertions in the step definitions:
    cucumberTestImplementation 'junit:junit:4.13.2'
    cucumberTestImplementation "org.gebish:geb-core:${gebVersion}"
    cucumberTestImplementation "org.seleniumhq.selenium:selenium-support:${seleniumVersion}"
    cucumberTestImplementation "org.seleniumhq.selenium:selenium-firefox-driver:${seleniumVersion}"
    cucumberTestImplementation "org.seleniumhq.selenium:selenium-chrome-driver:${seleniumVersion}"
    cucumberTestImplementation "io.github.bonigarcia:webdrivermanager:4.+"
    cucumberTestImplementation 'org.awaitility:awaitility-groovy:4.2.+'
    cucumberTestImplementation "org.gebish:geb-spock:${gebVersion}"
    cucumberTestImplementation "org.gebish:geb-junit4:${gebVersion}"
    cucumberTestImplementation 'io.cucumber:cucumber-groovy:6.1.+'
    cucumberTestImplementation 'org.apache.commons:commons-text:1.9'
}
