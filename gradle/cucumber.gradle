
addCucumberSuite 'cucumberTest'

//dockerCompose.isRequiredBy(cucumberTest)

cucumber {
    maxParallelForks = 1
    junitReport = true
}

cucumberTest {
    stepDefinitionRoots = ['cucumber.steps', 'cucumber.hooks'] // default

    doFirst {
        def host = '127.0.0.1'
        def port = 8080
        if (dockerCompose.servicesInfos) {
            host = dockerCompose.servicesInfos.app.firstContainer.host
            port = dockerCompose.servicesInfos.app.firstContainer.ports[8080]
        }
        URL url = new URL("http://${host}:${port}")

        systemProperties = [
                'app_proto': url.protocol,
                'app_host': url.host,
                'app_port': url.port,
                'app_path': url.path,
                'geb.build.baseUrl': url.toExternalForm(),
                'selenium.version' : seleniumVersion,
                'selenium.chromedriver.version' : seleniumChromeDriverVersion,

        ]
    }
}

dependencies {
    cucumberTestCompile 'io.cucumber:cucumber-java:6.10.+'
    // To use JUnit assertions in the step definitions:
    cucumberTestCompile 'junit:junit:4.13.2'
    cucumberTestCompile "org.gebish:geb-core:${gebVersion}"
    cucumberTestCompile "org.seleniumhq.selenium:selenium-support:${seleniumVersion}"
    cucumberTestCompile "org.seleniumhq.selenium:selenium-firefox-driver:${seleniumVersion}"
    cucumberTestCompile "org.seleniumhq.selenium:selenium-chrome-driver:${seleniumVersion}"
    cucumberTestCompile "io.github.bonigarcia:webdrivermanager:4.+"
    cucumberTestCompile 'org.awaitility:awaitility-groovy:4.0.+'
    cucumberTestCompile "org.gebish:geb-spock:${gebVersion}"
    cucumberTestCompile "org.gebish:geb-junit4:${gebVersion}"
    cucumberTestCompile 'io.cucumber:cucumber-groovy:6.1.+'
    cucumberTestCompile 'org.apache.commons:commons-text:1.9'
}
